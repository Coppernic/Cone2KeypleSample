plugins {
    id "fr.coppernic.versioning" version "3.1.2"
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply from: rootProject.file('gradle/ktlint.gradle')
apply from: rootProject.file('gradle/simple-sign.gradle')
apply from: rootProject.file('gradle/publish-app.gradle')

repositories {
    mavenLocal()
    google()
    jcenter()
    mavenCentral()
    maven { url "https://nexus.coppernic.fr/repository/maven-public" }
    // Keyple snaphots and releases
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 34

    defaultConfig {
        applicationId "fr.coppernic.sample.keyple"
        minSdkVersion 27
        targetSdkVersion 31
        versionCode versioning.info.versionCode
        versionName versioning.info.display

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Sometimes we are including libs that contains packages that are not in Android framework
    lintOptions {
        warning 'InvalidPackage'
    }

    // Needs this for Robolectric 4+
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Used for including libs compiled with kotlin
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
        exclude 'META-INF/lib_release.kotlin_module'
        exclude 'META-INF/NOTICE.md'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:1.6.0"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc03'
    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0-rc03"

    // Coppernic
    implementation 'fr.coppernic.sdk.ask:CpcAsk:3.5.8-keyple'
    implementation 'fr.coppernic.sdk.cpcutils:CpcUtilsLib:6.19.1'
    implementation 'fr.coppernic.sdk.core:CpcCore:2.2.6'

    // Keyple
    implementation group: 'org.eclipse.keyple', name: 'keyple-android-plugin-cone2', version:'0.8.1-20240910'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.bastienpaulfr:Treessence:1.0.5'
    implementation 'com.arcao:slf4j-timber:3.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Koin
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    //noinspection GradleDependency 4.0.x is conflicting with hamcrest and needs java 8
    testImplementation "org.awaitility:awaitility:3.0.0"
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.amshove.kluent:kluent:1.65'
    testImplementation 'androidx.test:core:1.2.0'

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    //noinspection GradleDependency 4.0.x is conflicting with hamcrest and needs java 8
    androidTestImplementation "org.awaitility:awaitility:3.0.0"
    androidTestImplementation('com.schibsted.spain:barista:3.2.0') {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
}

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute module("fr.bipi.treessence:treessence:0.3.0") with module('com.github.bastienpaulfr:Treessence:1.1.2')
    }
}
